plugins {
    id 'java'
}

group = 'org.kaorimatz.rundeck.jenkins'
version = '0.1.0-SNAPSHOT'
description = 'A Rundeck plugin to build Jenkins jobs'

sourceCompatibility = 1.8

configurations {
    lib
    compile {
        extendsFrom lib
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.11.5'
    lib(group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6') {
        exclude group: 'commons-codec'
        exclude group: 'commons-logging'
    }
}

jar {
    into('lib') {
        from configurations.lib
    }
    manifest {
        attributes 'Implementation-Title': 'rundeck-jenkins-plugin'
        attributes 'Implementation-Version': version
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Classnames': [
                'org.kaorimatz.rundeck.jenkins.JenkinsBuildStepPlugin',
                'org.kaorimatz.rundeck.jenkins.JenkinsWebhookNotificationPlugin'
        ].join(',')
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': '1.2'
        attributes 'Rundeck-Plugin-Libs': configurations.lib.collect { 'lib/' + it.name }.join(' ')
    }
}
